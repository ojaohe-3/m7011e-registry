plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.hibernate.orm' version '6.5.3.Final'
    id 'org.graalvm.buildtools.native' version '0.10.3'
    id 'org.liquibase.gradle' version '2.2.0'
    id "pl.allegro.tech.build.axion-release" version "1.18.7"
}

ext {
    springCloudVersion = "2023.0.3"
}

scmVersion {
    versionIncrementer('incrementPatch')
    ignoreUncommittedChanges.set(false)
    tag {
        versionIncrementer('incrementPatch')
        prefix.set('v')

    }
}

group = 'm7011e.the-homeric-odyssey'
project.version = scmVersion.version

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'

    project.version = rootProject.version
}


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    bom
    compileOnly.extendsFrom(bom)
    annotationProcessor.extendsFrom(bom)
    implementation.extendsFrom(bom)
    liquidsbase.extendsFrom(bom)
}

repositories {
    mavenCentral()

    maven {
        allowInsecureProtocol = true

        url("${reposiliteUrl}/snapshots")
        name "Reposilite"
        credentials(PasswordCredentials) {
            username = reposiliteUser
            password = reposiliteSecret
        }
        authentication {
            basic(BasicAuthentication)
        }
    }

    maven {
        allowInsecureProtocol = true

        url("${reposiliteUrl}/releases")
        name "Reposilite"
        credentials(PasswordCredentials) {
            username = reposiliteUser
            password = reposiliteSecret
        }
        authentication {
            basic(BasicAuthentication)
        }
    }
}

publishing {
    repositories {
        maven {
            allowInsecureProtocol = true
            if(project.version.toString().endsWith('-SNAPSHOT')) {
                url ("${reposiliteUrl}/snapshots")
            } else {
                url ("${reposiliteUrl}/releases")
            }
            name "Reposilite"
            credentials(PasswordCredentials) {
                username = reposiliteUser
                password = reposiliteSecret
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {

        maven(MavenPublication) {
            groupId = group
            version = version
            from components.java
        }
    }

}

dependencies {

    implementation platform(group: 'm7011e.the-homeric-odyssey', name: 'common-dependencies', version:'0.1.0')
    api 'm7011e.the-homeric-odyssey:resource-server'
    api 'm7011e.the-homeric-odyssey:authentication-components'
    api 'org.springframework.boot:spring-boot-starter-actuator'
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api 'org.liquibase:liquibase-core'
    api 'org.springframework.session:spring-session-core'
    api 'org.springframework.cloud:spring-cloud-starter-openfeign'
    api(group: 'org.modelmapper', name: 'modelmapper', version: '3.2.1')
    api 'org.springframework.boot:spring-boot-starter-data-rest'
    api 'org.springframework.boot:spring-boot-starter-security'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    runtimeOnly 'org.postgresql:postgresql'

    liquibaseRuntime 'org.liquibase:liquibase-core:4.24.0'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.1.1'
    liquibaseRuntime 'info.picocli:picocli:4.7.5'
    liquibaseRuntime 'org.yaml:snakeyaml:2.0'
    liquibaseRuntime 'org.postgresql:postgresql'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.apache.groovy:groovy:4.0.15'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.3-groovy-4.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.14.9'
    testImplementation 'com.athaydes:spock-reports:2.5.1-groovy-4.0'
}



hibernate {
    enhancement {
        enableAssociationManagement = true
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}


tasks.named('test') {
    useJUnitPlatform()

    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}
