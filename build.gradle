plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.hibernate.orm' version '6.5.3.Final'
    id 'org.graalvm.buildtools.native' version '0.10.3'
    id 'org.liquibase.gradle' version '2.2.0'
    id "pl.allegro.tech.build.axion-release" version "1.18.7"
}

ext {
    springCloudVersion = "2023.0.3"
}

scmVersion {
    versionIncrementer('incrementPatch')
    ignoreUncommittedChanges.set(false)
    tag {
        versionIncrementer('incrementPatch')
        prefix.set('v.')

    }
}

group = 'm7011e.the-homeric-odyssey'
project.version = scmVersion.version

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'

    project.version = rootProject.version
}


java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url reposiliteUrl
        name "Reposilite"
        credentials(PasswordCredentials) {
            username = reposiliteUser
            password = reposiliteSecret
        }
        authentication {
            basic(BasicAuthentication)
        }
    }
}

publishing {
    repositories {
        maven {
            if(project.version.toString().endsWith('-SNAPSHOT')) {
                url ("${reposiliteUrl}/snapshots")
            } else {
                url ("${reposiliteUrl}/releases")
            }
            name "Reposilite"
            credentials(PasswordCredentials) {
                username = reposiliteUser
                password = reposiliteSecret
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {

        maven(MavenPublication) {
            groupId = group
            version = version
            from components.java
        }
    }

}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.springframework.session:spring-session-core'
    implementation group: 'commons-io', name: 'commons-io', version: '2.17.0'
    implementation group: 'org.modelmapper', name: 'modelmapper', version: '3.2.1'
    implementation group: 'io.openapitools.swagger', name: 'swagger-maven-plugin', version: '2.1.6'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.9.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.cloud:spring-cloud-function-web'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    annotationProcessor 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'

    liquibaseRuntime 'org.liquibase:liquibase-core:4.24.0'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.1.1'
    liquibaseRuntime 'info.picocli:picocli:4.7.5'
    liquibaseRuntime 'org.yaml:snakeyaml:2.0'
    liquibaseRuntime 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.apache.groovy:groovy:4.0.15'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.3-groovy-4.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.14.9'
    testImplementation 'com.athaydes:spock-reports:2.5.1-groovy-4.0'
}



hibernate {
    enhancement {
        enableAssociationManagement = true
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}


tasks.named('test') {
    useJUnitPlatform()

    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}
